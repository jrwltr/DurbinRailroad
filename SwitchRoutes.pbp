;=============================================================================
route_delete_switch:
    if route_switch_index != $ff then
        if route_num_switches = 1 then
            route_switch_index = $ff
        elseif route_switch_index = route_num_switches - 1 then
            route_switch_index = route_switch_index - 1
        else
            for k = route_switch_index to route_num_switches - 2
                route_switches[k] = route_switches[k+1]
            next k
        endif
        route_num_switches = route_num_switches - 1
    endif
    return

;=============================================================================
route_error:
    DisplayRow = 1
    DisplayCol = 0
    gosub display_set_cursor
    gosub display_write_string
    gosub display_clear_to_end_of_line
    pause 2000
    return

;=============================================================================
route_activate:
    if route_num_switches != 0 then
        kk var byte
        for kk = 0 to route_num_switches - 1
            loconet_switch = route_switches[kk] & $7fff
            gosub switch_eeprom_find_translation
            if !switch_not_defined then
                if route_switches[kk] & $8000 then
                    switch_motor_state = SWITCH_MOTOR_THROWN
                else
                    switch_motor_state = SWITCH_MOTOR_CLOSED
                endif
                gosub switch_eeprom_write_switch_state
                gosub switch_bank_write
            endif
        next kk
    endif
    return

;=============================================================================
route_previous_switch:
    if route_switch_index != $ff and route_switch_index != 0 then
        route_switch_index = route_switch_index - 1
        loconet_switch = route_switches[route_switch_index] & $7fff
    endif
    return

;=============================================================================
route_next_switch:
    if route_switch_index != $ff and route_switch_index < route_num_switches-1 then
        route_switch_index = route_switch_index + 1
        loconet_switch = route_switches[route_switch_index] & $7fff
    endif
    return

;=============================================================================
route_cancel:
    loconet_switch = route_number
    route_to_find = route_number
    gosub route_eeprom_find
    if (route_found = ROUTE_NOT_FOUND) then
        route_number = ROUTE_NOT_FOUND
    else
        gosub route_eeprom_read
    endif
    return

;=============================================================================
route_create:
    route_switch_index = $ff
    route_num_switches = 0
    return

;=============================================================================
route_initialize:
    route_number = ROUTE_NOT_FOUND
    goto route_eeprom_count_routes

;=============================================================================

