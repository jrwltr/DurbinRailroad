;=============================================================================
#define DO_TURNTABLE
#define DO_LOCONET
#define DO_DISPLAY
#define DO_OPERATE_PROGRAM
#define DO_ONBOARD_SWITCHES
#define DO_EEPROM
#define DO_LONG_EEPROM_FUNCTIONS

;=============================================================================

CLEAR       ; zero all variables

INCLUDE "ProcessorConfiguration.pbp"

goto init   ; skip over interrupt service routine, functions, other included code

INCLUDE "Interrupt.pbp"

;=============================================================================
operate_mode        var byte
    MODE_OPERATE    con 0
    MODE_PROGRAM    con 1
    MODE_MOVING     con 2

loconet_switch      var word

display_update_needed   var bit

CalibrateState  var bit[1]
    CAL_SET_ZERO    con 0
    CAL_SET_180     con 1

;=============================================================================

INCLUDE "LCD4X20.pbp"
INCLUDE "EEPROM.pbp"
INCLUDE "OnboardSwitches.pbp"
INCLUDE "LOCONET.inc"
INCLUDE "LOCONET.pbp"
INCLUDE "LongMath.pbp"
INCLUDE "TurnTableEEPROM.pbp"
INCLUDE "TurnTable.pbp"

;=============================================================================
; display_long: write a long value to the display
;   intput: long_minuend - the value to be displayed
;
display_long:
    if (long_minuend[0] = $ffff) and (long_minuend[1] = $ffff) then
        ; no translation found
        ARRAYWRITE DisplayString, ["??? ", 0]
    else
        long_subtrahend[0] = TT_ZERO_OFFSET_LO
        long_subtrahend[1] = TT_ZERO_OFFSET_HI
        gosub long_subtract
        ARRAYWRITE DisplayString, [hex long_result[1], hex4 long_result[0], " ", 0]
    endif
    goto display_write_string

;=============================================================================
update_display:
    DisplayRow = 0
    DisplayCol = 0
    gosub display_set_cursor
    if loconet_switch != $ffff then
        ARRAYWRITE DisplayString, [dec loconet_switch+1, " ", 0]
    else
        ARRAYWRITE DisplayString, ["??? ", 0]
    endif
    gosub display_write_string

    long_minuend[0] = desired_tt_position[0]
    long_minuend[1] = desired_tt_position[1]
    gosub display_long

    long_minuend[0] = current_tt_loc[0]
    long_minuend[1] = current_tt_loc[1]
    gosub display_long

    gosub display_clear_to_end_of_line

    DisplayCol = DISPLAY_COLUMNS-1
    gosub display_set_cursor
    if operate_mode = MODE_OPERATE then
        ARRAYWRITE DisplayString, ["O", 0]
    elseif operate_mode = MODE_PROGRAM then
        ARRAYWRITE DisplayString, ["P", 0]
    endif
    gosub display_write_string

    DisplayRow = 1
    DisplayCol = 0
    gosub display_set_cursor
    if operate_mode = MODE_OPERATE then
        ARRAYWRITE DisplayString, ["UP  DWN  GO", 0]
        if (tt_180[0] != 0) or (tt_180[1] != 0) then
            gosub display_write_string
            ARRAYWRITE DisplayString, ["  180", 0]
        endif
    elseif operate_mode = MODE_PROGRAM then
        ARRAYWRITE DisplayString, ["CW CCW SET CAL", 0]
    elseif operate_mode = MODE_MOVING then
        ARRAYWRITE DisplayString, ["Moving", 0]
    endif
    gosub display_write_string
    goto display_clear_to_end_of_line


;=============================================================================
operate_switch_on:
    if operate_mode != MODE_OPERATE then 
        operate_mode = MODE_OPERATE
    endif
    return

operate_switch_off:
    if operate_mode != MODE_PROGRAM then 
        operate_mode = MODE_PROGRAM
    endif
    return


;=============================================================================
button_1_pressed:
    if operate_mode = MODE_OPERATE then
operate_up_key:
        gosub turntable_previous_location
    elseif operate_mode = MODE_PROGRAM then
        gosub turntable_clockwise
    endif
    return

button_2_pressed:
    if operate_mode = MODE_OPERATE then
operate_down_key:
        gosub turntable_next_location
    elseif operate_mode = MODE_PROGRAM then
        gosub turntable_counterclockwise
    endif
    return

button_3_pressed:
    ; enter key
    if operate_mode = MODE_OPERATE then
        position_to_goto[0] = desired_tt_position[0]
        position_to_goto[1] = desired_tt_position[1]
        goto turntable_goto_position
    elseif operate_mode = MODE_PROGRAM then
        goto turntable_set_translation
    else
        return
    endif

button_4_pressed:
    if operate_mode = MODE_OPERATE then
        if (tt_180[0] != 0) or (tt_180[1] != 0) then
            long_minuend[0] = current_tt_loc[0]
            long_minuend[1] = current_tt_loc[1]
            long_subtrahend[0] = tt_180[0]
            long_subtrahend[1] = tt_180[1]
            gosub long_subtract
            position_to_goto[0] = long_result[0]
            position_to_goto[1] = long_result[1]
            if (long_result[1] & $8000) then
                ; current_tt_loc is less than 180 degrees, move clockwise
                long_addend1[0] = current_tt_loc[0]
                long_addend1[1] = current_tt_loc[1]
                long_addend2[0] = tt_180[0]
                long_addend2[1] = tt_180[1]
                gosub long_add
                position_to_goto[0] = long_result[0]
                position_to_goto[1] = long_result[1]
            endif
            goto turntable_goto_position
        endif
    elseif operate_mode = MODE_PROGRAM then
        CalibrateState = CAL_SET_ZERO
    endif
    return

;=============================================================================
button_1_repeat:
    if operate_mode = MODE_OPERATE then
        goto operate_up_key
    endif
    goto button_cancel_repeat

button_2_repeat:
    if operate_mode = MODE_OPERATE then
        goto operate_down_key
    endif
    goto button_cancel_repeat

button_3_repeat:
    goto button_cancel_repeat

button_4_repeat:
    if operate_mode = MODE_PROGRAM then
        CalibrateState = CAL_SET_180
    endif
    goto button_cancel_repeat

;=============================================================================
button_1_released:
button_2_released:
    if operate_mode = MODE_MOVING then
        goto turntable_stop
    endif
button_3_released:
    return
button_4_released:
    if operate_mode = MODE_PROGRAM then
        if (CalibrateState = CAL_SET_ZERO) then
            gosub turntable_set_zero
        elseif (CalibrateState = CAL_SET_180) then
            gosub turntable_set_180
        endif
    endif
    return

;=============================================================================
init:
gosub display_initialize
#ifdef DO_LED
gosub led_initialize
#endif
gosub loconet_initialize 
gosub turntable_initialize

;=============================================================================

INCLUDE "Mainloop.pbp"

;=============================================================================

    End

