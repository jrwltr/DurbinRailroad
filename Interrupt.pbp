;=============================================================================
RXFIFO_SIZE con 16

#ifdef DO_LOCONET
rx_data_fifo        var byte[RXFIFO_SIZE] bank0 SYSTEM
rx_head             var byte bank0 SYSTEM
rx_tail             var byte bank0 SYSTEM
#endif

#ifdef DO_RS485
rs485_rx_data_fifo  var byte[RXFIFO_SIZE] bank0 SYSTEM
rs485_rx_head       var byte bank0 SYSTEM
rs485_rx_tail       var byte bank0 SYSTEM
#endif

;=============================================================================
TimerTickInterrupt  var byte bank0 SYSTEM

#if __PROCESSOR__ = "18F13K22"
ASM

CheckTimerTick macro
    banksel INTCON
    btfss   INTCON,2                        ;
    bra     tt                              ; if INTCON.TMR0IF = 1
    bcf     INTCON,2                        ;    INTCON.TMR0IF = 0
    clrf    BSR                             ;    set bank to 0
    movlw   1                               ;    TimerTickInterrupt = 1
    movwf   TimerTickInterrupt              ;
tt:                                         ; endif
    endm

ENDASM
#else
ASM

CheckTimerTick macro
    banksel PIR0
    btfss   PIR0,5                          ;
    bra     tt                              ; if PIR0.TMR0IF = 1
    bcf     PIR0,5                          ;    PIR0.TMR0IF = 0
    clrf    BSR                             ;    set bank to 0
    movlw   1                               ;    TimerTickInterrupt = 1
    movwf   TimerTickInterrupt              ;
tt:                                         ; endif
    endm

ENDASM
#endif

;=============================================================================
#ifdef DO_TURNTABLE
HalfSecondInterrupt var byte bank0 SYSTEM
ASM
CheckTurntableTimer  macro
    banksel PIR4
    btfss   PIR4,4                          ;
    bra     ttt1                            ; if PIR4.TMR5IF = 1
    bcf     PIR4,4                          ;    PIR4.TMR5IF = 0
ttt1:                                       ; endif
    btfss   PIR4,2                          ;
    bra     ttt2                            ; if PIR4.TMR3IF = 1
    bcf     PIR4,2                          ;    PIR4.TMR3IF = 0
    clrf    BSR                             ;    set bank to 0
    movlw   1                               ;    HalfSecondInterrupt = 1
    movwf   HalfSecondInterrupt             ;
ttt2:                                       ; endif
    endm
ENDASM
#else
ASM
CheckTurntableTimer  macro
    endm
ENDASM
#endif

;=============================================================================
fsr0l_save      var byte bank0 SYSTEM
fsr0h_save      var byte bank0 SYSTEM

ASM

InterruptEntry  macro
    clrf    BSR                             ; set bank to 0
    movf    FSR0L,W                         ;
    movwf   fsr0l_save                      ; fsr0l_save = fsr0l
    movf    FSR0H,W                         ;
    movwf   fsr0h_save                      ; fsr0h_save = fsr0h
    endm

InterruptExit   macro
    clrf    BSR                             ; set bank to 0
    movf    fsr0l_save,  W                  ; fsr0l = fsr0L_save
    movwf   FSR0L                           ;
    movf    fsr0h_save,  W                  ; fsr0h = fsr0H_save
    movwf   FSR0H                           ;
    endm

ENDASM

;=============================================================================
ASM
CheckReceiveInterrupt   macro PIR, RC_IF, RC_STA, RC_REG, data_fifo, rx_head, rx_tail
    local rcv1
    local rcv2
    local rcv3
    local rcv4
    local rcv5
    local rcv6
rcv1:                                       ;
    banksel PIR                             ;
    btfss   PIR,RC_IF                       ;
    bra     rcv6                            ; while PIR.RCIF = 1
    banksel RC_STA                          ;
    btfss   RC_STA, 2                       ;     if RCSTA.FERR then
    bra     rcv2                            ;         framing error
    movf    RC_REG, W                       ;         W = RC_REG
    bcf     RC_STA, 7                       ;         RC1STA.7 = 0 ; SPEN = 0 reset the serial port
    bsf     RC_STA, 7                       ;         RC1STA.7 = 1 ; SPEN = 1
                                            ;         could notify the upper layer but protocol doesn't require it
    bra     rcv6                            ;         break;
rcv2:                                       ;
    btfss   RC_STA, 1                       ;     elseif RC1STA.OERR then
    bra     rcv3                            ;         overrun error
    bcf     RC_STA, 4                       ;         RC1STA.4 = 0 ; CREN = 0 to clear error
    bsf     RC_STA, 4                       ;         RC1STA.4 = 1 ; CREN = 1
                                            ;         could notify the upper layer but protocol doesn't require it
    bra     rcv6                            ;         break;
rcv3:                                       ;     else
    clrf    BSR                             ;         set bank to 0
    movf    rx_tail, W                      ;         W = rx_tail
    addlw   data_fifo                       ;         W = W + data_fifo
    movwf   FSR0L                           ;         FSR0L = W
    clrf    FSR0H                           ;         FSR0H = 0
    movf    rx_tail, W                      ;
    addlw   1                               ;
    subwf   rx_head, W                      ;
    btfss   STATUS, 2                       ;         if rx_tail + 1 == rx_head then
    bra     rcv4                            ;
                                            ;             fifo full
                                            ;             could notify the upper layer but protocol doesn't require it
    banksel RC_REG                          ;
    movf    RC_REG, W                       ;             W = RCREG
    bra     rcv1                            ;
rcv4:                                       ;         else
    movf    rx_tail, W                      ;
    addlw   1                               ;
    movwf   rx_tail                         ;             rx_tail = rx_tail + 1
    sublw   _RXFIFO_SIZE                    ;
    btfss   STATUS, 2                       ;             if rx_tail == RXFIFO_SIZE
    bra     rcv5                            ;
    movlw   0                               ;
    movwf   rx_tail                         ;                 rx_tail = 0
rcv5:                                       ;             endif
    banksel RC_REG                          ;
    movf    RC_REG, W                       ;             W = RCREG
    clrf    BSR                             ;             set bank to 0
    movwf   INDF0                           ;             store rx data in fifo
                                            ;         endif
    bra     rcv1                            ;     endif
rcv6:                                       ; endwhile
    endm

ENDASM

;=============================================================================
#ifdef DO_LOCONET
#if __PROCESSOR__ = "18F13K22"
ASM
CheckLoconetReceiveInterrupt macro
    ;we don't do loconet on this processor (switch extension)
    endm
ENDASM
#else
ASM
CheckLoconetReceiveInterrupt macro
    CheckReceiveInterrupt   PIR3, 5, RC1STA, RC1REG, rx_data_fifo,      rx_head,        rx_tail
    endm
ENDASM
#endif
#else
ASM
CheckLoconetReceiveInterrupt macro
    endm
ENDASM
#endif

;=============================================================================
#ifdef DO_RS485
#if __PROCESSOR__ = "18F13K22"
ASM
CheckRS485ReceiveInterrupt macro
    CheckReceiveInterrupt   PIR1, 5, RCSTA,  RCREG,  rs485_rx_data_fifo, rs485_rx_head, rs485_rx_tail
    endm
ENDASM
#else
ASM
CheckRS485ReceiveInterrupt macro
    CheckReceiveInterrupt   PIR3, 7, RC2STA, RC2REG, rs485_rx_data_fifo, rs485_rx_head, rs485_rx_tail
    endm
ENDASM
#endif
#else
ASM
CheckRS485ReceiveInterrupt macro
    endm
ENDASM
#endif

;=============================================================================
define INTHAND  isr

ASM
isr:
    InterruptEntry
;following debug lines toggle PORTC.3 on every interrupt
;    banksel LATC
;    movlw   8
;    xorwf   LATC, 1
    CheckLoconetReceiveInterrupt
    CheckRS485ReceiveInterrupt
    CheckTimerTick
    CheckTurntableTimer
    InterruptExit
    retfie
ENDASM

;=============================================================================

